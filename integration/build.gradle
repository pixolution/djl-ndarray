plugins {
    id 'application'
    id 'jacoco'
}

dependencies {
    implementation "commons-cli:commons-cli:${commons_cli_version}"
    implementation "org.apache.logging.log4j:log4j-slf4j-impl:${log4j_slf4j_version}"
    implementation project(":testing")


    runtimeOnly project(":engines:tensorflow:tensorflow-engine")
}

java {
    compileJava {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
        options.compilerArgs.removeAll(["--release", "8"])
    }
}

tasks.register('copyDependencies', Copy) {
    into "build/dependencies"
    from configurations.runtimeClasspath
}

application {
    mainClass = System.getProperty("main", "ai.djl.integration.IntegrationTest")
}

run {
    environment("TF_CPP_MIN_LOG_LEVEL", "1") // turn off TensorFlow print out
    systemProperties System.getProperties()
    systemProperties.remove("user.dir")
    systemProperty("file.encoding", "UTF-8")
    jvmArgs "-Xverify:none"
}

tasks.register('debugEnv', JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    systemProperties System.getProperties()
    systemProperties.remove("user.dir")
    systemProperties.put("ai.djl.logging.level", "debug")
    mainClass = "ai.djl.integration.util.DebugEnvironment"
}

tasks.distTar.enabled = false
